// Процедура отправки форматированного сообщения через SMTP сервер
//
// *** Важные моменты:
// 1) Файлы - Массив структур. Структура состоит из 2-х полей. 'Имя' - Строка (отображаемое имя файла. Можно задать любум), 'ПолныйПуть' - Строка (путь к файлу в системе или во временном хранилище).
//	Имя может быть любым, но не забывайте про расширение файла.
// 2) HTML и Картинки можно получить из форматированного текста 1с за счёт метода 'ПолучитьHTML(HTML, Картинки)'. Перед использованием объявить переменные 'HTML' и 'Картинки'.
// 3) Вставлять картинки в форматированный текст нужно с помощью специальной кнопки, а не перетаскиванием. Особенность 1с, кек... Ломается код при drag and drop. 
// 4) Если в массиве 'Получатели' находится пустая строка или несуществующий email, то он игнорируется.
// Параметры:
//   Тема				- Строка	- Тема сообщения.
//   HTML				- Строка	- Строка, содержащая html документ со всеми тегами. Обычно формируется автоматически из объекта 'Форматированный текст'.
//   Картинки			- Структура	- Структура, где ключ - название картинки, значение - сама картинка. Обычно формируется из 'форматированного текста' за счёт метода ТекстСообщения.ПолучитьHTML(HTML, Картинки).  
//   Получатели			- Массив	- Массив строк с email адресами получателей рассылки.
//   АдресОтправителя	- Строка	- email адрес отправителя (отображается в заголовке письма). Можно задать любой даже не существующий (example@gmail.com). 
//   ИмяОтправителя		- Строка	- Имя отправителя, которое отобразится вместо email адреса отправителя.
//	 Файлы				- Массив	- Массив структур. Структура состоит из 'Имя' - Строка (отображаемое имя файла. Можно задать любым), 'ПолныйПуть' - Строка (путь к файлу в системе или во временном хранилище).
//
Процедура ОтправкаФорматированногоСообщения(Тема, HTML, Картинки, Получатели, АдресОтправителя = "1cmailing@bseu.by", ИмяОтправителя = "1C Admin", Файлы = Неопределено) Экспорт
	ИПП = Новый ИнтернетПочтовыйПрофиль;
	ИПП.АдресСервераSMTP = "IP_addr";
	ИПП.ПарольSMTP = "pass";
	ИПП.ПользовательSMTP = "user";
	ИПП.ПортSMTP = "port";
	ИПП.ИспользоватьSSLSMTP = Истина;
	
	Если Не ЗначениеЗаполнено(АдресОтправителя) Тогда
		АдресОтправителя = "1cmailing@bseu.by";
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИмяОтправителя) Тогда
		ИмяОтправителя = "1C Admin";
	КонецЕсли;
	
	ПочтовоеСообщение = Новый ИнтернетПочтовоеСообщение;
	ПочтовоеСообщение.Кодировка = "UTF-8";
	Для Каждого Получатель Из Получатели Цикл
		ПочтовоеСообщение.Получатели.Добавить(Получатель);
	КонецЦикла;
	ПочтовоеСообщение.Отправитель.Адрес = АдресОтправителя;
	ПочтовоеСообщение.Отправитель.ОтображаемоеИмя = ИмяОтправителя;
	ПочтовоеСообщение.Важность = ВажностьИнтернетПочтовогоСообщения.Высокая;
	ПочтовоеСообщение.Тема = Тема;
	
	Для Каждого Картинка Из Картинки Цикл
	    Вложение = ПочтовоеСообщение.Вложения.Добавить(Картинка.Значение.ПолучитьДвоичныеДанные());
	    Вложение.Идентификатор = Картинка.Ключ;
	    HTML = СтрЗаменить(HTML, Картинка.Ключ, "cid:" + Вложение.Идентификатор);
	КонецЦикла;
	ТекстСообщения = ПочтовоеСообщение.Тексты.Добавить(HTML, ТипТекстаПочтовогоСообщения.HTML);
	
	Если ТипЗнч(Файлы) = Тип("Массив") Тогда
		Для Каждого Файлик Из Файлы Цикл
			Попытка
				ПочтовоеСообщение.Вложения.Добавить(Файлик.ПолныйПуть, Файлик.Имя);
			Исключение
				Сообщить("Не удалось добавить вложение '" + Файлик.Имя + "'");
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Почта = Новый ИнтернетПочта;
	Почта.Подключиться(ИПП);
	Почта.Послать(ПочтовоеСообщение);
	Почта.Отключиться();	
КонецПроцедуры

// Процедура отправки простого сообщения через SMTP сервер
//
// *** Важные моменты:
// 1) Файлы - Массив структур. Структура состоит из 2-х полей. 'Имя' - Строка (отображаемое имя файла. Можно задать любум), 'ПолныйПуть' - Строка (путь к файлу в системе или во временном хранилище).
//		Имя может быть любым, но не забывайте про расширение файла.
// 2) Если в массиве 'Получатели' находится пустая строка или несуществующий email, то он игнорируется.
// Параметры:
//   Тема				- Строка	- Тема сообщения.
//   Текст				- Строка	- Строка с текстом сообщения.  
//   Получатели			- Массив	- Массив строк с email адресами получателей рассылки.
//   АдресОтправителя	- Строка	- email адрес отправителя (отображается в заголовке письма). Можно задать любой даже не существующий (example@gmail.com). 
//   ИмяОтправителя		- Строка	- Имя отправителя, которое отобразится вместо email адреса отправителя.
//	 Файлы				- Массив	- Массив структур. Структура состоит из 'Имя' - Строка (отображаемое имя файла. Можно задать любым), 'ПолныйПуть' - Строка (путь к файлу в системе или во временном хранилище).
//
Процедура ОтправкаСообщения(Тема, Текст, Получатели, АдресОтправителя = "1cmailing@bseu.by", ИмяОтправителя = "1C Admin", Файлы = Неопределено)
	ИПП = Новый ИнтернетПочтовыйПрофиль;
	ИПП.АдресСервераSMTP = "IP_addr";
	ИПП.ПарольSMTP = "pass";
	ИПП.ПользовательSMTP = "user";
	ИПП.ПортSMTP = "port";
	ИПП.ИспользоватьSSLSMTP = Истина;
	
	Если Не ЗначениеЗаполнено(АдресОтправителя) Тогда
		АдресОтправителя = "1cmailing@bseu.by";
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ИмяОтправителя) Тогда
		ИмяОтправителя = "1C Admin";
	КонецЕсли;
	
	ПочтовоеСообщение = Новый ИнтернетПочтовоеСообщение;
	ПочтовоеСообщение.Кодировка = "UTF-8";
	Для Каждого Получатель Из Получатели Цикл
		ПочтовоеСообщение.Получатели.Добавить(Получатель);
	КонецЦикла;
	ПочтовоеСообщение.Отправитель.Адрес = АдресОтправителя;
	ПочтовоеСообщение.Отправитель.ОтображаемоеИмя = ИмяОтправителя;
	ПочтовоеСообщение.Важность = ВажностьИнтернетПочтовогоСообщения.Высокая;
	ПочтовоеСообщение.Тема = Тема;
	ТекстСообщения = ПочтовоеСообщение.Тексты.Добавить(Текст);
	
	Если ТипЗнч(Файлы) = Тип("Массив") Тогда
		Для Каждого Файлик Из Файлы Цикл
			Попытка
				ПочтовоеСообщение.Вложения.Добавить(Файлик.ПолныйПуть, Файлик.Имя);
			Исключение
				Сообщить("Не удалось добавить вложение '" + Файлик.Имя + "'");
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	
	Почта = Новый ИнтернетПочта;
	Почта.Подключиться(ИПП);
	Почта.Послать(ПочтовоеСообщение);
	Почта.Отключиться();	
КонецПроцедуры
